!===============================================================================
! 20 April 2020 - Fabian Krajewski ITB mbH
!		Tel.: +49 (0)231 94536534
!		This FE-model is part of the Framework service contract ITER/17/CT/6000000211/PMT
!		Task Order 1, Deliverable 1, 18 TF coil model
!
!		Version 3
! 
! This file is to Run Mechanical Modal analyses on MAGNET MODEL for dynamic studies
!
!===============================================================================
/NERR, ,99999999
!=======================================================================
! SET THE MAGNETIC NODAL PREFRENCES
!
/CONFIG, NRES, 4000 	!If time steps are too many, the number should be increased to store the results.
!===========================================================================
! CONSTANT
!
		TRUE			= 1
		FALSE			= 0
		
!===============================================================================
! ASSIGN OUTPUT
	
	Rev  		= %arg1%
!========================================================================
! set txt OUTPUT
!	
	/CWD, './MAGNET_%Rev%/TEXTOUT'
	/OUTPUT,MECHANICAL,txt
	/CWD, '../../'

!===============================================================================
! Enums (DO NOT MODIFY!)
!
	!=======================================================================
	! PARAMETERS
	!
	nCoil_MAG     	= 18
	nCoil_MECH     	= 18
	g		= 9.81		! gravity acceleration[m/s^2]
	nModes		= 60 		! N of modes to be extracted for modal analyses
	!=======================================================================
	! LOCAL COORDINATE SYSTEM ID
	!
	GlobCyl		= 11		! Global Cyl CS

	!=======================================================================
	! REFERENCE TEMPERATURES
	!
	RoomT		= 292		! [K]
	OperT		= 4		! [K]
	AnkerT		= 77	! [K]
	
	!=======================================================================
	! PRECOMPRESSION RINGS PRETENSIONING FORCES
	!
	! Changed from 3179046 to 2382000
	! FK, 15.06.2018
	F_PRET_PCR_M80	= 2382000	! [N]
	! Changed from 6010385 to 6810000
	! FK, 15.06.2018
	F_PRET_PCR_M110	= 6810000	! [N]
	PCR_BOLTS_KDIR	= 2.67		! [m] Point along the X axis (Local CS 11) for separation surface location for PCR bolts 
	
	!=======================================================================
	!CENTRAL SOLENOID PRETENSIONING FORCES
	!
	F_PRET_CS_FULL	= 210E6  	! [N]
	Num_CS_TIEP			= 18
	CS_PLATE_KDIR   = 0.0			! [m] Point along the Z axis (Local CS 11) for separation surface location for CS plates
	! TO FIT WITH FINE MODEL (LOAD CASE SHIMMING) FPRET IS REDUCED
	F_RED_FACTOR = 0.95 
	F_PRET_CS_TIEP		= F_PRET_CS_FULL/Num_CS_TIEP*F_RED_FACTOR
	
	!=======================================================================
	! OIS M110 and M130 PRETENSIONING FORCES
	F_PRET_OIS = 1.5E6 		! [N]
		
	!===========================================================================
	! GS BOTTUM BOLTS Pretension forces
	! M85 Clamping bolts, 2.45 [MN]
	F_PRET_GSBM85 = 2.45E6 		! [N]
	! M60 Pins, 1.21 [MN]
	F_PRET_GSBM60 = 1.21E6 		! [N]
	!===========================================================================
	! GS UPPER BOLTS Pretension forces
	! M80 Clamping bolts, 0.52 [MN]
	F_PRET_GSUM80 = 5.2E5 		! [N]
	! M60 Pins, 0.32 [MN]
	F_PRET_GSUM60 = 3.2E5 		! [N]
	
		
	!===========================================================================
	! ELEMENT TYPE ID
	!
	ET_SOLID		= 1
	ET_MASS			= 2
	ET_C_STAND		= 3
	ET_T_STAND		= 4
	ET_C_BOND		= 5
	ET_T_BOND		= 6
	ET_C_SLID		= 7
	ET_T_SLID		= 8
	ET_C_ST_NO_GAP		= 9
	ET_T_ST_NO_GAP		= 10
	ET_C_MPC		= 11  
	ET_T_MPC		= 12  
	ET_MPC184		= 13
	ET_LINK			= 14
	ET_SOLSH		= 15
	ET_BEAM			= 16
	ET_SPRING		= 17	

!===============================================================================
!===============================================================================
!===============================================================================
	
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	!
	! CHECK_POINT_1
	! (select HERE: Revision, and Case)
	!
	!		
	Rev  		= %arg1%				
	Case 		= %arg2%
	!
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§	
	
	!===========================================================================
	! SET SCENARIO
	!
	*IF,arg6,EQ,0,THEN
		*IF, %arg3%, EQ, 1, THEN
		
			Scenario = 'TF_ONLY'
		
		*ELSEIF, %arg3%, EQ, 2, THEN
		
			Scenario = 'SOD'
		
		*ELSEIF, %arg3%, EQ, 3, THEN
		
			Scenario = 'SOP'
		
		*ELSEIF, %arg3%, EQ, 4, THEN
			
			Scenario = 'XPF'
		
		*ELSEIF, %arg3%, EQ, 5, THEN
		
			Scenario = 'CS1_0'
		
		*ELSEIF, %arg3%, EQ, 6, THEN

			Scenario = 'CS2U0'
		
		*ELSEIF, %arg3%, EQ, 7, THEN

			Scenario = 'CS2L0'
		
       		*ELSEIF, %arg3%, EQ, 8, THEN
		
			Scenario = 'SOF'
		
		*ELSEIF, %arg3%, EQ, 9, THEN
		
			Scenario = 'SOB'
		
		*ELSEIF, %arg3%, EQ, 10, THEN
		
			Scenario = 'EOB'
		
		*ELSEIF, %arg3%, EQ, 11, THEN
		
			Scenario = 'EOP'
		
		*ELSEIF, %arg3%, EQ, 12, THEN
		
			Scenario = 'EOB+PD'
	
		*ENDIF
	*ELSEIF,arg6,EQ,1,THEN
		*IF, %arg3%, EQ, 1, THEN
		
			Scenario = 'TF_ONLY'
		
		*ELSEIF, %arg3%, EQ, 2, THEN
		
			Scenario = 'SOD'
		
		*ELSEIF, %arg3%, EQ, 3, THEN
		
			Scenario = 'SOFT'
		
		*ELSEIF, %arg3%, EQ, 4, THEN
		
			Scenario = 'EOFT'
		*ENDIF
	*ENDIF

!===============================================================================
!===============================================================================
!===============================================================================

	!=======================================================================
	! DO NOT MODIFY (unless you need to add other cases)
	!
			
		!==================================================================================================================================
		! THERMAL ANALYSIS - evaluate Temperature Field ON GRAVITY SUPPORT
		!
		*IF,		Case, EQ, 0, THEN
			Model		= 'MAGNET_MECH'		! TH MODEL
			CaseName	= 'TH_GRAV_SUPP'	! the case name
			THERMAL_GS	= TRUE			! Perform GRAVITY SUPPORT THERMAL ANALYSIS 		
			KIND		= 'THERMAL'
				
		!==================================================================================================================================
		! MECHANICAL ANALYSIS - COOL DOWN (from LS1 to LS2)
		!
		*ELSEIF,	Case, EQ, 1, THEN
			Model		= 'MAGNET_MECH'		! MECH MODEL
			CaseName	= 'COOL_DOWN'		! the case name
			COOL_DOWN	= TRUE			! Perform NORMAL OPERATION ANALYSIS		
			KIND		= 'MECHANICAL'										
		
		!==================================================================================================================================
		! MECHANICAL ANALYSIS - Normal operation
		!
		*ELSEIF,	Case, EQ, 2, THEN
			Model		= 'MAGNET_MECH'	! MECH MODEL
			CaseName	= Scenario		! the case name
			ENRG		= TRUE			! Perform TF ONLY			
			KIND		= 'MECHANICAL'
                *ELSE
			/EOF
		*ENDIF
	
!===============================================================================
!===============================================================================
! ASSIGN DIRECTORY, TITLE, CASENAME AND Load MECH or TH DB model 
	
	!========================================================================
	! set filename
	!
	/FILNAME,'MAGNET_MECH',1
	
	!========================================================================
	! set txt OUTPUT
	!
	/CWD, './MAGNET_%Rev%/TEXTOUT'
	/OUTPUT,'%CaseName%',txt
	/CWD, '../../'
	
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	!
	! CHECK_POINT_2
	! (select HERE: change to PROPER DIRECTORY)
	!
	!
	/CWD,		'./MAGNET_%Rev%/%KIND%'
	!
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	
	!========================================================================
	! READ model
	!		
	*IF, ENRG, EQ, TRUE, THEN
		RESUME,'%Model%', db, , 1, 1	
	*ELSE
		RESUME,'../_models/%Model%', db, , 1, 1
	*ENDIF	
	
	ALLSEL, ALL
	
	!=========================================================================
	! set title
	!
	/TITLE,MAGNET_MECHANICAL_Rev%Rev%::%CaseName%

!===============================================================================
!===============================================================================


!===============================================================================
!===============================================================================
!
! THERMAL ANALYSES
!
!===============================================================================
!===============================================================================
!
*IF, THERMAL_GS, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! PRE-PROCESSING
	!
	/PREP7	
		!===========================================================================
		! Deactivate shape test on elems 
		!
		SHPP, OFF
		
		!===========================================================================
		! SELECT CONTACT ELEMENTS
		!
		CMSEL, S, E_TH_MODEL
		NSLE
		ESLN
		ESEL,R,ENAM,,170
		ESEL,U,TYPE,,1,1000
		CM,TMP_TARG,ELEM
		CMSEL,S,GS_BOLTS
		NSLE
		ESLN
		ESEL,U,ENAM,,179
		CM,TMP_CONT,ELEM
		
		
		!===========================================================================
		! DELETE NO-NEEDED COMPONENTS
		!
		ALLSEL, ALL
		CPDELE, ALL
		CMSEL, U, E_TH_MODEL
		CMSEL, U, TMP_TARG
		CMSEL, U, TMP_CONT
		NSLE,S
		EDELE, ALL
		NDELE, ALL
		
		!===========================================================================
		! Fix Unit System
		!
		EMUNIT, MKS		! EM UNITS SYSTEM
		
		!===========================================================================
		! DEFINE AND SET SOLID5 KOPT
		! 
		ET, ET_SOLSH, SOLID70
		ET, ET_SOLID, SOLID70				! THERMA 8-NODE SOLID
		ET, ET_LINK, LINK33
		ET, ET_BEAM, LINK33
		ET, 41, SOLID70						! THERMA 8-NODE SOLID
		ET, 42, SOLID70	
		ET, 43, SOLID70
		ET, 44, SOLID70
		ET, 45, SOLID70
		
		*DO,i,4008,9334,2
			KEYOPT, i, 1, 2
		*ENDDO

		KEYOPT, ET_C_BOND, 1, 2
		KEYOPT, ET_C_ST_NO_GAP, 1, 2

		!===========================================================================
		! MODIFY BEARINGS CONTACT
		! 
		CMSEL, S, C71
		EMODIF, ALL, TYPE, ET_C_BOND
		CMSEL, S, T71
		EMODIF, ALL, TYPE, ET_T_BOND

		!===========================================================================
		! DELETE MECHANICAL ET
		!
		ETDELE, 2, 4, 1
		ETDELE, 7, 8, 1
		ETDELE, 16, 16, 1
		RDELE, 1000, 1122, 1
		RDELE, 1141, 3000, 1
		
		!===========================================================================
		! Reduce component N_operating_temp
		! Fabian Krajewski 28.05.2018
		CSYS,1
		CMSEL, S, n_operating_temp
		NSEL, S, LOC, X, 10.135, 10.154
		NSEL, R, LOC, Z, -3.66, -3.65
		CM, TMPNODE1, NODE
		NSEL, S, LOC, X, 10.535, 10.545
		NSEL, R, LOC, Z, -2.94, -2.93
		CM, TMPNODE2, NODE
		NSEL, S, LOC, X, 10.675, 10.685
		NSEL, R, LOC, Z, -2.62, -2.61
		CM, TMPNODE3, NODE
		NSEL, S, LOC, X, 10.79, 10.808
		NSEL, R, LOC, Z, -2.30, -2.29
		CM, TMPNODE4, NODE
		NSEL, S, LOC, X, 10.89, 10.91
		NSEL, R, LOC, Z, -1.99, -1.995
		CM, TMPNODE5, NODE
		NSEL, S, LOC, X, 10.15, 10.17
		NSEL, R, LOC, Z, -3.64, -3.63
		CM, TMPNODE6, NODE
		NSEL, S, LOC, X, 10.21, 10.225
		NSEL, R, LOC, Z, -3.55, -3.54
		CM, TMPNODE7, NODE
		NSEL, S, LOC, X, 10.93, 10.95
		NSEL, R, LOC, Z, -1.845, -1.855
		CM, TMPNODE8, NODE
		NSEL, S, LOC, X, 10.00, 10.02
		NSEL, R, LOC, Z, -3.87, -3.86
		CM, TMPNODE9, NODE
		NSEL, S, LOC, X, 10.115, 10.132
		NSEL, R, LOC, Z, -3.696, -3.690
		CM, TMPNODE10, NODE		
		NSEL, S, LOC, X, 10.295, 10.31
		NSEL, R, LOC, Z, -3.397, -3.396
		CM, TMPNODE11, NODE	
		NSEL, S, LOC, X, 10.46, 10.47
		NSEL, R, LOC, Z, -3.095, -3.085
		CM, TMPNODE12, NODE	
		NSEL, S, LOC, X, 10.603, 10.618
		NSEL, R, LOC, Z, -2.78, -2.775
		CM, TMPNODE13, NODE	
		NSEL, S, LOC, X, 10.735, 10.748
		NSEL, R, LOC, Z, -2.46, -2.455
		CM, TMPNODE14, NODE	
		NSEL, S, LOC, X, 10.82, 10.863
		NSEL, R, LOC, Z, -2.13, -2.135
		CM, TMPNODE15, NODE
		! Define new component n_operating_temp
		CMSEL, S, TMPNODE1
		*DO, X, 2, 15
			CMSEL, A, TMPNODE%X%
		*ENDDO
		CM, n_operating_temp, NODE
		!===========================================================================
		! Define component N_anker_temp
		! Fabian Krajewski 28.05.2018
		NSEL, S, LOC, Z,-5.5915,-5.582
		CM, n_anker_temp, NODE
		!===========================================================================
		

	FINISH	
	!===========================================================================
	!===========================================================================
	! SOLUTION
	!
	/SOL
	BCSOPTION,,INCORE
        !===========================================================================
		! APPLY FIXED TEMPERATURE
		!	
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_ROOM_TEMP
		D, ALL, TEMP, RoomT
		
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_OPERATING_TEMP
		D, ALL, TEMP, OperT
		
		!  ADDITIONAL THERMAL ANKER
		!  Fabian Krajewski, 28.05.2018
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_ANKER_TEMP
		D, ALL, TEMP, AnkerT
	
		!===========================================================================
		! SOLVE
		!	
		ALLSEL, ALL
		SOLVE
	
	FINISH
	
	/dele,,esav
	/dele,,emat
	/dele,,r001
	/dele,,rdb
	/dele,,full
	
	!==================================================================================================================================
	! POST - WRITE TEMPERATURE FIELD FILE FOR GRAVITY SUPPORT
	!
	/POST1

		!===========================================================================
		! write temperature file
		!
		NSEL, NONE
		ALLSEL, ALL
		NSLE, ALL
		*GET,nN,NODE,,COUNT
		*GET,iN,NODE,,NUM,MIN
		*CFOPEN, '_OUT/GS_T_FIELD', temp
			*DO,i,1,nN
				*GET, T, NODE, iN ,TEMP,          
				*VWRITE,'BF,', iN, ', TEMP,', T
%C %8I %C %12.5E

				*GET,iNnew,NODE,iN,NXTH
				iN=iNnew
			*ENDDO
		*CFCLOSE
      	FINISH
	!===============================================================================
	! SAVE DB
	!
	SAVE
	
*ENDIF

!===========================================================================
!===========================================================================
!
! MECHANICAL ANALYSES
!
!===========================================================================
!===========================================================================
!===========================================================================

	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	!
	! CHECK_POINT_3
	! (select HERE: OutFlag)
	!
	OutFlag = %arg5%
	!
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§

!===========================================================================
!===========================================================================
! NORMAL OPERATION
!
*IF, COOL_DOWN, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! PRE-PROCESSING
	!
	/PREP7	
		
		!===========================================================================
		! UPGRADE E_SOLIDS
		ESEL, S, ENAM,, 185,186
		ESEL, A, ENAM,, 190
		CM, E_SOLIDS, ELEM
		!===========================================================================
		! ROTATE CS FOR DOF CONSTRAINED NODES
		!	
		ESEL, NONE
		NSEL, NONE
		CSYS, GlobCyl
		CMSEL, S, N_GS_DOF
		NROTAT, ALL
		CSYS, 0
		ESEL, NONE
		NSEL, NONE
		
		!===========================================================================
		! MODIFY CONTACT REAL 4001 and 4007, higher friction and bonded contact
	!	ESEL, S, REAL,,4007
	!	EMODIF,ALL,MAT,40
	!	MP,MU,40,0.8
	!	cid = 27
	!	ESEL, S, REAL,,4001
	!	keyopt,cid,12,5
		!===========================================================================
		! FIX CS PRET ELEMENT IN TOROIDAL DIRECTION
		CSYS, 1
		ESEL, S, MAT,,37
		NSLE
		ESEL, R, CENT, X, 0, 2.05
		! LOWER BLOCK
		NSLE
		NSEL, R, LOC, Z,  -6.549, -6.547 
		NROTAT, ALL		
		CM, ntmp, NODE
		*DO, i, 1, 9
			CMSEL, S, ntmp
			NSEL, R, LOC, Y, (i-1)*40
			D, ALL, UY, 0
		*ENDDO
		! UPPER BLOCK
		NSLE
		NSEL, R, LOC, Z,    6.60,   6.61 
		NROTAT, ALL		
		CM, ntmp, NODE
		*DO, i, 1, 9
			CMSEL, S, ntmp
			NSEL, R, LOC, Y, (i-1)*40
			D, ALL, UY, 0
		*ENDDO
		! CREATE COMBIN14 CONNECTION
		!*GET, NUMTYPE, ELEM, 0, TYPM
		!NUMREAL = 950
		!NUMTYPE = NUMTYPE + 1
		!ET,NUMTYPE,COMBIN14
		!TYPE, NUMTYPE
		!REAL, NUMREAL

		!===========================================================================
		! CONSTRAIN ON GRAV SUPP RING
		!
		CMSEL, S, N_GS_DOF
		D, ALL, UX, 0.0
		D, ALL, UY, 0.0
		D, ALL, UZ, 0.0
		ESEL, NONE
		NSEL, NONE	
		
		!===========================================================================
		! BCs ON BEAM188
		!===========================================================================

		!===========================================================================
		! CREATE LOACAL CS (1 FOR EACH SHEAR KEYS GROUP) AND ROTATE LINK NODAL COORDINATE SYSTEM AND APPLY BC
		!	
		ESEL, S, TYPE, , ET_BEAM
		NSLE, S
		D, ALL, ROTX, 0.0		
		D, ALL, ROTY, 0.0	
		D, ALL, ROTZ, 0.0			
		CSYS, 0
		
	FINISH

	!===========================================================================
	!===========================================================================
	! LS1 STATIC ANALYSIS: PRET + DW
	!
	/SOL
	BCSOPTION,,INCORE
        !===========================================================================
		! LS1: PCR PRET + TF DW (WITHOUT PFs AND CS) - SOLUTION SETTINGS
		!===========================================================================
		!
		/TITLE,  'LS1:76 MN radial force + DW'
		ANTYPE, STATIC, 
		SOLCONTROL, ON, ON
		AUTOTS, ON                         	! AUTO time stepping
		NLDIAG, CONT, ITER                 	! DISPLAY CONTA STATUS AT EACH ITERATION
		NLDIAG, NRRE, 1, 30					! WRITE NEWTON RAPHSON RESIDUAL FILES
!		CONVTOL, F, , 0.001					! Lower force residual criterion
		NSUBST, 2, 50, 1
		NEQIT, 50				
		OUTRES, ALL, LAST
		!===========================================================================
		! REFERENCE AND UNIFORM TEMPERATURE
		!		
		ALLSEL,ALL
		TREF, RoomT				! reference Temperature = 292 [K]
		TUNIF, RoomT				! operating Temperature = 292 [K]
		
		!===============================================================================
		! APPLY PRETENSIONING ON PRE-COMPR. RING BOLTS
		!
		
		!===========================================================================
		! READ PRE-COMPR. RING BOLT SETTINGS
		!===========================================================================
		!===========================================================================
		*DIM, PCR_FACTOR, ARRAY, 18, 4
		*VREAD, PCR_FACTOR(1,1),   '../_models/PARAMETER/PRET_FACTOR_18_Coils', txt, , JIK, 4 ,18 , , 3
(4F8.1)

		PCRREV = %arg4%
		!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
		
		nBOLT = 2*nCoil_MECH
		*DO, i, 1, 8
			
			*IF, i, EQ, 1, OR, i, EQ, 4, THEN
				F_PRET = F_PRET_PCR_M80
			*ELSEIF, i, EQ, 5, OR, i, EQ, 8, THEN
				F_PRET = F_PRET_PCR_M80
			*ELSE
				F_PRET = F_PRET_PCR_M110
			*ENDIF
			tmpNumber = 1
			*DO, j, 1, nBOLT
				*IF,tmpNumber,EQ,1,THEN
					*IF,j,EQ,1,THEN
					PRET_FACTOR = PCR_FACTOR(1,PCRREV+2)
					*ELSE
					PRET_FACTOR = PCR_FACTOR((j+1)/2,PCRREV+2)
					*ENDIF
				*ENDIF
				F_PRET_N = (F_PRET*PRET_FACTOR)
				*SET,F_PRET_N, F_PRET*PRET_FACTOR
                                ! Original line was *SET,F_PRET_N, F_PRET * PRET_FACTOR but PRET_FCATOR was not used.
				! Do not use spaces around operation symbols, as “ *”
                                ! a space and a star makes the remainder of the line a comment
				ID = j+(36*(i-1))
				SLOAD,ID,PL01,LOCK,FORC, F_PRET_N, 1, 2
				tmpNumber = tmpNumber-0.5
				*IF,tmpNumber,EQ,0,THEN
				tmpNumber = 1
				*ENDIF
			*ENDDO
		*ENDDO
		
		!===============================================================================
		! APPLY PRETENSIONING ON CS TIE PLATES
		!
		*DO, i, 1, nCoil_MECH
			ID = ID+i
			F_PRET = F_PRET_CS_TIEP
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO
		
		!===============================================================================
		! APPLY PRETENSIONING ON OIS
		!
		ID = 1009
		nbolt = nCOil_MECH*24
		ALLSEL
		*DO, i, 1, nbolt
			ID = ID+1
			F_PRET = F_PRET_OIS
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO	
		!===============================================================================
		! APPLY PRETENSIONING ON GS BOTTOM CLAMPING BOLTS
		!
		nbolt = nCOil_MECH*12
		*DO, i, 1, nbolt
			ID = ID+1
			F_PRET = F_PRET_GSBM85
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO
		!===============================================================================
		! APPLY PRETENSIONING ON GS BOTTOM PINS
		!
		nbolt = nCOil_MECH*14
		*DO, i, 1, nbolt
			ID = ID+1
			F_PRET = F_PRET_GSBM60
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO
		!===============================================================================
		! APPLY PRETENSIONING ON GS UPPER CLAMPING BOLTS
		!
		nbolt = nCOil_MECH*12
		*DO, i, 1, nbolt
			ID = ID+1
			F_PRET = F_PRET_GSUM80
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO
		!===============================================================================
		! APPLY PRETENSIONING ON GS UPPER INSULATED BOLTS
		!
		nbolt = nCOil_MECH*12
		*DO, i, 1, nbolt
			ID = ID+1
			F_PRET = F_PRET_GSUM60
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2 		
		*ENDDO
				
		!===============================================================================
		! APPLY GRAVITY ON ALL COMPONENTS (EXCLUDING PFs)
		!
		ALLSELL, ALL
        	CMACEL, E_SOLIDS, , , g	
		
		!===============================================================================
		! KILL PF COILS
		!
		CMSEL, S, PF1
		CMSEL, A, PF2
		CMSEL, A, PF3
		CMSEL, A, PF4
		CMSEL, A, PF5
		CMSEL, A, PF6
		EKILL, ALL
			
		!===============================================================================
		! SOLVE
		!
		ALLSELL, ALL
		TIME, 1
		SOLVE	

		
		!===========================================================================
		! LS2: COOL DOWN
		!===========================================================================
		!
		/TITLE,  'TF_LS2:76 MN radial force + DW + CD'
		ALLSEL, ALL
		TREF, RoomT				! reference Temperature = 292 [K]	
		TUNIF, OperT				! operating Temperature = 4 [K]	
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, E_TH_MODEL
		NSLE, S
		*MSG, INFO,
                Start of reading file with temperatures
                /NOPR,
                /INPUT, '../THERMAL/_OUT/GS_T_FIELD', temp
		/GOPR,
                !===============================================================================
		! KILL PF COILS
		!
		CMSEL, S, PF1
		CMSEL, A, PF2
		CMSEL, A, PF3
		CMSEL, A, PF4
		CMSEL, A, PF5
		CMSEL, A, PF6
		EALIVE, ALL

		!===============================================================================
		! SOLVE
		!
		NSUBST, 2, 10, 1
		ALLSEL, ALL
		TIME, 2
		SOLVE
		
	FINISH
	
	!===============================================================================
	! SAVE DB
	!
	SAVE
	
	! ===========================================================
	! Delete unneeded files if this was the last calculation step
	*IF,     OutFlag, EQ, 0, THEN
	/dele,,esav
	/dele,,ldhi
	/dele,,emat
	/dele,,osav
	/dele,,r001
	/dele,,rdb
	/dele,,full
	*DO,TMP,0,19
		/dele,MAGNET_MECH%TMP%,emat
		/dele,MAGNET_MECH%TMP%,esav
		/dele,MAGNET_MECH%TMP%,full
		/dele,MAGNET_MECH%TMP%,page
		/dele,MAGNET_MECH%TMP%,rst
		/dele,MAGNET_MECH%TMP%,stat
		*DO,TMP1,1,9
			/dele,MAGNET_MECH%TMP%,r00%TMP1%
		*ENDDO
		*DO,TMP1,10,15
			/dele,MAGNET_MECH%TMP%,r0%TMP1%
		*ENDDO
	*ENDDO
	*ELSEIF, OutFlag, EQ, 1, THEN
	*ENDIF
	! ===========================================================
	
	
	
*ENDIF

!===========================================================================
!===========================================================================
! ENERGIZING
!
*IF, ENRG, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! CONTINUE SOLUTION APPLYING LORENTZ FORCES FOR DIFFERENT SCENARIOS
	!	
	/SOL
	BCSOPTION,,INCORE
        
		
		*GET, LStime, ACTIVE, 0, SET, TIME
				
		!===========================================================================
		! SETTINGS
		!
		PARSAV, SCALAR, ScalarPARAM, PARM
		*IF,arg6,EQ,0,AND,arg3,EQ,12,THEN
		/ASSIGN, RST, 'MAGNET_EOB',rst
		*ELSE
		/ASSIGN, RST, 'MAGNET_MECH',rst	
		*ENDIF
		
		ANTYPE, STATIC, REST, LAST, , CONTINUE           
		PARRES, CHANGE, ScalarPARAM, PARM
		/TITLE,  LS %Scenario%:76 MN radial force + DW + CD + %Scenario%
		! SET PF COILS ALIVE
		!
		CMSEL, S, PF1
		CMSEL, A, PF2
		CMSEL, A, PF3
		CMSEL, A, PF4
		CMSEL, A, PF5
		CMSEL, A, PF6
		EALIVE, ALL
		ALLSEL, ALL
	
		TIME, arg3+2

		!===========================================================================
		! CONVERGENCE SETTINGS
		!
		SOLCONTROL, ON, ON
		AUTOTS, ON                              ! AUTO time stepping
		NLDIAG, CONT, ITER                      ! DISPLAY CONTA STATUS AT EACH ITERATION
		NLDIAG, NRRE, 1, 30						! WRITE NEWTON RAPHSON RESIDUAL FILES
		NSUBST, 2, 50, 1
		*IF,arg6,EQ,0,AND,arg3,EQ,12,THEN
			NSUBST, 3, 50, 1
		*ENDIF
		NEQIT, 30		

		!===========================================================================
		! APPLY LORENTZ FORCES FROM MAG ANALYSIS
		!
		ALLSELL, ALL
		*MSG, INFO,
                Start of reading file with forces
                /NOPR,
				*IF, ARG6, EQ, 0, THEN
					/INPUT, '../../MAG_INPUT/V3/15MA/%Scenario%_TF', forces
					*IF, arg3, GE, 2, THEN
						/INPUT, '../../MAG_INPUT/V3/15MA/%Scenario%_PF', forces
						/INPUT, '../../MAG_INPUT/V3/15MA/%Scenario%_CS', forces
					*ENDIF
				*ELSEIF, ARG6, EQ, 1, THEN
					/INPUT, '../../MAG_INPUT/V3/5MA/%Scenario%_TF', forces
					*IF, arg3, GE, 2, THEN
						/INPUT, '../../MAG_INPUT/V3/5MA/%Scenario%_PF', forces
						/INPUT, '../../MAG_INPUT/V3/5MA/%Scenario%_CS', forces
					*ENDIF
				*ENDIF
		/GOPR,
                SOLVE

	FINISH

	!===============================================================================
	! SAVE DB
	!
	SAVE

	! SAVE EOB RST FOR EOB+PD
	*IF,arg6,EQ,0,AND,arg3,EQ,10,THEN
		*IF, Outflag, EQ, 0, THEN
		*ELSEIF,Outflag,EQ,1,THEN
			/COPY, './MAGNET_MECH', rst, , './MAGNET_EOB', rst
!
! NEXT THREE COMMNADS ARE REQUIRED WHEN DISTRIBUTED SOLVER IS USED BUT
! THAT OPTION DOES NOT WORK WITH SOLID5 ELEMENTS IN A EM ANALYSIS. IF THE FILES WITH LORENTZ FORCES ARE AVAILABLE
! THE DISTRIBUTED SOLVER SHOULD WORK.
!		*DO,TMP,0,19
!		/COPY, './MAGNET_MECH%TMP%', rst, , './MAGNET_EOB%TMP%', rst
!		*ENDDO
		*ENDIF
	*ENDIF

	!============================================================
	! POST 
	! ASSIGN LOADCASE EOB+PD to MAGNET_MECH.rst
	*IF,arg6,EQ,0,AND,arg3,EQ,12,THEN
		/POST1
		FILE,'MAGNET_EOB','rst','.'
		set,,,,,14
		FILE,'MAGNET_MECH','rst','.'
		RAPPEND, 14, 14
	*ENDIF

	! ===========================================================
	! Delete unneeded files if this was the last calculation step
	*IF,     OutFlag, EQ, 0, THEN
	/dele,,esav
	/dele,,ldhi
	/dele,,emat
	/dele,,osav
	/dele,,rdb
	/dele,,full
	/DELE,MAGNET_EOB,rst
		
	*DO,TMP,1,9
		/dele,,r00%TMP%
	*ENDDO
	*DO,TMP,0,19
		/dele,MAGNET_MECH%TMP%,emat
		/dele,MAGNET_MECH%TMP%,esav
		/dele,MAGNET_MECH%TMP%,full
		/dele,MAGNET_MECH%TMP%,page
		/dele,MAGNET_MECH%TMP%,rst
		/dele,MAGNET_MECH%TMP%,stat
		/dele,MAGNET_EOB%TMP%,rst
		*DO,TMP1,1,9
			/dele,MAGNET_MECH%TMP%,r00%TMP1%
		*ENDDO
		*DO,TMP1,10,15
			/dele,MAGNET_MECH%TMP%,r0%TMP1%
		*ENDDO
	*ENDDO

	*ELSEIF, OutFlag, EQ, 1, THEN
	*ENDIF
	! ===========================================================

*ENDIF
/EOF





	