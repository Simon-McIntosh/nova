!===============================================================================
!
! 17 AUGUST 2020 - Fabian Krajewski ITB mbH
!		Tel.: +49 (0)231 94536534
!		This FE-model is part of the Framework service contract ITER/17/CT/6000000211/PMT
!		Task Order 4, Deliverable 2, Two TF coil model
!
!		Version 4.0 (TF_COILS_V9_4.cdb)
!
! 		This macro is to Run mechanical and thermal analyses for the 2 coil TFC model
!
!		Following arguments are used for this macro
!   	arg1
!  TYPE OF MODEL, allow to choose the model to be ran 
!	arg1=1 -> TF1-SINGLE COIL MODEL TYPE B (20 deg, symmetry plane <-10 deg;+10deg>)
!   arg1=2 -> TF2-SINGLE COIL MODEL TYPE A (20 deg, symmetry plane <+10 deg;+30deg>)
!   arg1=3 -> TF1&TF2-TWO COILS MODEL      (40 deg, symmetry plane <-10 deg;+30deg>)
!
!		arg2
!  Revision
!
!		arg3
!  Counter for scenario 
!	arg3 = 0, Thermal or cooldown
!	arg3 = 1, TF-ONLY
!	arg3 = 2, SOD
!		...
!
!		arg4
!  TYPE OF CALCULATION
!	arg4 = 1, THERMAL
!	arg4 = 2, COOLDOWN
!	arg4 = 3, OPERATIONAL
!===============================================================================

!=======================================================================
! SET THE MAGNETIC NODAL PREFRENCES
!

/CONFIG, NRES, 4000 	!If time steps are too many, the number should be increased to store the results.

!===========================================================================
! CONSTANT
!
		TRUE			= 1
		FALSE			= 0
		
! Enums (DO NOT MODIFY!)
!

	!=======================================================================
	! material Ids (as they are labeled in the ".mats" file)
	!
	MAT_WP	 	= 1		! SMEARED MATERIAL FOR WP
	MAT_SS		= 2		! SS316 LN-IG
	MAT_RING	= 3		! SMEARED MATERIAL FOR PRECOMPRESSION RING
	MAT_AIR		= 4		! SMEARED MATERIAL FOR PRECOMPRESSION RING
	MAT_INCONEL	= 5		! INCONEL FOR PINS
	MAT_G10		= 6		! G10
	MAT_SHEAR_KEYS	= 7		! MATERIAL FOR SHEAR KEYS LINK180
	MAT_FRICT_RING	= 8		! FRICTION FOR RING
	MAT_FRIC_MID_A  = 9		! FRICTION FOR WEDGING Plane +10 degree
	MAT_FRIC_MID_B  = 31		! FRICTION FOR WEDGING Plane -10 degree
        MAT_FRICT_WP	= 10		! FRICTION FOR WP
	MAT_FRICT_GS	= 11		! FRICTION FOR GS
	MAT_FRICT_IOIS	= 12		! FRICTION FOR GS
	MAT_FRICT_OIS	= 13		! FRICTION FOR GS
	MAT_FRICT_BEAR  = 14		! FRICTION FOR RING BEARINGS

	!=======================================================================
	! PARAMETERS
	!
	g		= 9.81		! gravity acceleration[m/s^2]

	!=======================================================================
	! LOCAL COORDINATE SYSTEM ID
	!
	GlobCyl		= 11		! Global Cyl CS
	SymCart		= 12		! Cartesian System on symmetry plane between 2 coils
	HalfCart	= 13		! Cartesian System on half plane of coil 1

	U_OIS_SYS_1 	= 1001		! Local UPPER OIS CS (-10 deg plane) for bolts (X AXIS = PINS axis)
	L_OIS_SYS_1 	= 1002		! Local LOWER OIS CS (-10 deg plane) for bolts (X AXIS = PINS axis)

	UN_IOIS_SYS_1	= 2001		! Local UPPER IOIS CS (-10 deg plane) for pins (X AXIS = PINS axis)
	UP_IOIS_SYS_1	= 2002		! Local UPPER IOIS CS (+10 deg plane) for pins (X AXIS = PINS axis)
	LN_IOIS_SYS_1	= 2003		! Local UPPER IOIS CS (-10 deg plane) for pins (X AXIS = PINS axis)
	LP_IOIS_SYS_1	= 2004		! Local UPPER IOIS CS (+10 deg plane) for pins (X AXIS = PINS axis)

	U_GS_SYS_1 	= 3001		! Local UPPER GS CS for bolts (X AXIS = PINS axis)
	L_GS_SYS_1 	= 3002		! Local LOWER GS CS for bolts (X AXIS = PINS axis)

	U_OIS_SYS_2 	= 1003		! Local UPPER OIS CS (+10 deg plane) for bolts (X AXIS = PINS axis)
	L_OIS_SYS_2 	= 1004		! Local LOWER OIS CS (+10 deg plane) for bolts (X AXIS = PINS axis)

	UN_IOIS_SYS_2	= 2005		! Local UPPER IOIS CS (+10 deg plane) for pins (X AXIS = PINS axis)
	UP_IOIS_SYS_2	= 2006		! Local UPPER IOIS CS (+30 deg plane) for pins (X AXIS = PINS axis)
	LN_IOIS_SYS_2	= 2007		! Local UPPER IOIS CS (+10 deg plane) for pins (X AXIS = PINS axis)
	LP_IOIS_SYS_2	= 2008		! Local UPPER IOIS CS (+30 deg plane) for pins (X AXIS = PINS axis)

	U_GS_SYS_2 	= 3003		! Local UPPER GS CS for bolts (X AXIS = PINS axis)
	L_GS_SYS_2 	= 3004		! Local LOWER GS CS for bolts (X AXIS = PINS axis)

	!=======================================================================
	! KDIR POINT TO BUILT PRETENSION SECTION
	!
	PCR_BOLTS_KDIR	=2.67		! [m] Point along the X axis (Local CS 11) for separation surface location for PCR bolts
	OIS_KDIR 	=0.1075		! [m] Point along the X axis (Local CS 101,102) for separation surface location for OIS bolts
	IOIS_KDIR 	=0.205		! [m] Point along the X axis (Local CS 201,202,203,204) for separation surface location for IOIS pins
	GS_TOP_KDIR 	=0.0		! [m] Point along the X axis (Local CS 201,202,203,204) for separation surface location for IOIS pins
	GS_BOT_KDIR 	=0.0		! [m] Point along the X axis (Local CS 201,202,203,204) for separation surface location for IOIS pins

	!=======================================================================
	! REFERENCE TEMPERATURES
	!
	RoomT		= 292		! [K]
	OperT		= 4		! [K]
        AnchorT         = 77            ! [K]
	!=======================================================================
	! PRETENSION FORCES
	!
!	F_PRET_PCR_M80	= 2115000	! [N] PCR M80 INCONEL BOLTS (from IO)
!	F_PRET_PCR_M110	= 7078000	! [N] PCR M110 INCONEL BOLTS (from IO)
!
! Bolt preload in Pre-compression Ring Counter Flange is changed on 17 April 2018 into the value below
! Values are now in agreement with PA and drawings.
!
	F_PRET_PCR_M80  = 0.65*2382000	! [N] PCR M80 INCONEL BOLTS (from IO)
	F_PRET_PCR_M110 = 0.65*6810000	! [N] PCR M110 INCONEL BOLTS (from IO)

	F_PRET_UGS_M80  = 0.59E6	! [N] UPPER GS M80 SS BOLTS - WAS 0.79e6
	F_PRET_UGS_M60  = 0.32E6	! [N] UPPER GS M60 SS BOLTS - WAS 0.43e6

        F_PRET_LGS_M85  = 2.45E6	! [N] LOWER GS M85 INCONEL BOLTS - WAS 3.54E6
        F_PRET_LGS_M60  = 1.21E6	! [N] LOWER GS M60 INCONEL BOLTS - WAS 1.66E6

        F_PRET_OIS_M130 = 1.5E6		! [N] OIS M160 SS BOLTS (from DRAWING #003360)
        F_PRET_OIS_M110 = 1.5E6		! [N] OIS M120 SS BOLTS (from DRAWING #003360)

	F_PRET_IOIS     = 0.23E6	! [N] IOIS PINS (75% of Sy)
        
        F_PRET_PF1      = -(2*5*1.15e6) ! [N] Preload of PF1 clamps with TF-Coil
        F_PRET_PF6      = +(2*5*1.72e6) ! [N] Preload of PF6 clamps with TF-Coil
        MU_PF           = 0.2           ! Assumed Friction Coeffcient for radial frixtion force
	!===========================================================================
	! ELEMENT TYPE ID
	!
	ET_SOLID		= 1
	ET_CONTA178		= 2
	ET_C_STAND		= 3
	ET_T_STAND		= 4
	ET_C_MPC		= 5
	ET_T_MPC		= 6
	ET_C_SLID		= 7
	ET_T_SLID		= 8
	ET_MASS			= 9
	ET_SOLSH		= 10
	ET_LINK			= 11
	ET_MPC184		= 12
	ET_C_STAND_NOGAP	= 13
	ET_T_STAND_NOGAP	= 14
	ET_BEAM			= 15
    ET_SPRING		= 16

    ET_C_WP                 = 17
    ET_T_WP                 = 18
    ET_C_Bearing            = 19
    ET_T_Bearing            = 20

	ET_SOURC36_PF1   	= 101
	ET_SOURC36_PF2 		= 102
	ET_SOURC36_PF3 		= 103
	ET_SOURC36_PF4 		= 104
	ET_SOURC36_PF5 		= 105
	ET_SOURC36_PF6 		= 106
	ET_SOURC36_PLASMA 	= 107
	ET_SOURC36_TF_INNER	= 108
	ET_SOURC36_TF_OUTER	= 109
	ET_SOURC36_CSU3		= 110
	ET_SOURC36_CSU2		= 111
	ET_SOURC36_CSU1		= 112
	ET_SOURC36_CSL1		= 113
	ET_SOURC36_CSL2		= 114
	ET_SOURC36_CSL3		= 115

	!===========================================================================
	! SECTION ID
	!
	S_RINGS 		= 500

	!===========================================================================
	! PF AND CS OWN WEIGHT
	!
	F_CS_OW   =  -1.039*1.0e6    ! 953.5 tons
	F_PF1_OW  =  -0.155*1.0e6    ! 142.9 tons
        F_PF2_OW  =  -0.154*1.0e6    ! 141.1 tons
	F_PF3_OW  =  -0.342*1.0e6    ! 313.8 tons
	F_PF4_OW  =  -0.312*1.0e6    ! 286.8 tons
	F_PF5_OW  =  -0.264*1.0e6    ! 242.1 tons
        F_PF6_OW  =  -0.300*1.0e6    ! 275.3 tons

!===============================================================================
!===============================================================================
!===============================================================================

	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	!
	! CHECK_POINT_1
	! (select HERE: Revision, and Case)
	!
	!
	Rev  		= %arg2%
	Case 		= %arg4%
	!
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§

	!===========================================================================
	!===========================================================================
	! SET SCENARIO
	!
	*IF, %arg3%, EQ, 1, THEN

		Scenario = 'TF_ONLY'

	*ELSEIF, %arg3%, EQ, 2, THEN

		Scenario = 'SOD'

	*ELSEIF, %arg3%, EQ, 3, THEN

		Scenario = 'SOP'

	*ELSEIF, %arg3%, EQ, 4, THEN

		Scenario = 'XPF'

	*ELSEIF, %arg3%, EQ, 5, THEN

		Scenario = 'CS10'

        *ELSEIF, %arg3%, EQ, 6, THEN

		Scenario = 'CS2U0'

        *ELSEIF, %arg3%, EQ, 7, THEN

		Scenario = 'CS2L0'

        *ELSEIF, %arg3%, EQ, 8, THEN

		Scenario = 'SOF'

	*ELSEIF, %arg3%, EQ, 9, THEN

		Scenario = 'SOB'

	*ELSEIF, %arg3%, EQ,10, THEN

		Scenario = 'EOB'

	*ELSEIF, %arg3%, EQ,11, THEN

		Scenario = 'EOP'

	*ELSEIF, %arg3%, EQ,12, THEN

		Scenario = 'EOB+PD'

	*ENDIF

	!===========================================================================
	!===========================================================================
	!===========================================================================
	!===========================================================================
	! SET TYPE OF MODEL (i.e. TF TYPE A, TF TYPE B OR 2 COILS MODEL)
	!
	*IF, 	%arg1%, EQ, 1, THEN

		RefCaseName 	= 'TF_TYPE_1'

		nCoil		= 1
		Coil_1		= 1
		Coil_2		= 1
		
		alfa_1		= -10
		alfa_2		=  10

	*ELSEIF, %arg1%, EQ, 2, THEN
		
		RefCaseName     = 'TF_TYPE_2'
		
		nCoil		= 1
		Coil_1		= 2
		Coil_2		= 2
		
		alfa_1		=  10
		alfa_2		=  30
	
	*ELSEIF, %arg1%, EQ, 3, THEN
		
		RefCaseName     = 'TF1andTF2'
		
		nCoil		= 2
		Coil_1		= 1
		Coil_2		= 2

		alfa_1		= -10
		alfa_2		=  30
	
	*ENDIF

	!===========================================================================
	!===========================================================================

!===============================================================================
!===============================================================================
!===============================================================================

	!=======================================================================
	! DO NOT MODIFY (unless you need to add other cases)
	!
			
		!==================================================================================================================================
		! THERMAL ANALYSIS - evaluate Temperature Field ON GRAVITY SUPPORT
		!
		*IF,		Case, EQ, 1, THEN
			FEModel		= 'TF_COILS_TH'		! MAG MODEL
			CaseName	= 'TH_GRAV_SUPP'	! the case name
			THERMAL_GS	= TRUE			! Perform GRAVITY SUPPORT THERMAL ANALYSIS
			KIND		= 'THERMAL'		
			Label		= ' GS_THERMAL'
				
		!==================================================================================================================================
		! MECHANICAL ANALYSIS - COOL DOWN
		!
		*ELSEIF,	Case, EQ, 2, THEN
			FEModel		= 'TF_COILS_MECH'	! MECH MODEL
			CaseName	= 'COOL_DOWN'		! the case name
			COOL_DOWN	= TRUE			! Perform NORMAL OPERATION ANALYSIS
			KIND		= 'MECHANICAL'
		
		!==================================================================================================================================
		! MECHANICAL ANALYSIS - ENERGIZING (restart from cooldown applying different ENERGIZING scenarios)
		!
		*ELSEIF,	Case, EQ, 3, THEN
			FEModel		= 'MAGNET_MECH'	! MECH MODEL
			CaseName	= Scenario		! the case name
			ENRG		= TRUE			! Perform NORMAL OPERATION ANALYSIS			
			KIND		= 'MECHANICAL'
										
		*ELSE
			/EOF
		*ENDIF
	
!===============================================================================
!===============================================================================
! ASSIGN DIRECTORY, TITLE, CASENAME AND Load MECH or TH DB model 

	/FILENAME,'MAGNET_MECH',1
	
!========================================================================
! set txt OUTPUT
!	
	/CWD, './%RefCaseName%_Rev_%Rev%/TEXTOUT'
	/OUTPUT,%CaseName%,txt
	/CWD, '../../'
	
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§
	!
	! CHECK_POINT_2
	! (select HERE: change to PROPER DIRECTORY)
	!
	!
	/CWD, './%RefCaseName%_Rev_%Rev%/%KIND%'
	!
	!§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§§

	!========================================================================
	! READ model
	!		
	*IF, ENRG, EQ, TRUE, THEN
	
		RESUME,'%FEModel%', db, , 1, 1
	*ELSE
		RESUME,'../_models/%FEModel%', db, , 1, 1
	*ENDIF	

	ALLSEL, ALL
	
	!=========================================================================
	! set title
	!
	/TITLE,%RefCaseName%_Rev%Rev%  CaseName:%CaseName%  Scenario2:15MA Baseline 

!===============================================================================
!===============================================================================


!===============================================================================
!===============================================================================
!
! THERMAL ANALYSES
!
!===============================================================================
!===============================================================================
!
*IF, THERMAL_GS, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! PRE-PROCESSING
	!
	/FILENAME,'MAGNET_TH',1
	/PREP7
		!===========================================================================
		! Deactivate shape test on elems
		!
		SHPP, OFF
		
		!===========================================================================
		! Fix Unit System
		!
		EMUNIT, MKS		! EM UNITS SYSTEM
		

	FINISH
	!===========================================================================
	!===========================================================================
	! SOLUTION
	!
	/SOL
		BCSOPTION,,INCORE
		/ASSIGN, RST, 'MAGNET_TH',rst
		!===========================================================================
		! APPLY FIXED TEMPERATURE
		!	
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_ROOM_TEMP
		D, ALL, TEMP, RoomT
		
                ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_THERMAL_ANCHOR
		D, ALL, TEMP, AnchorT
		
                ESEL, NONE
		NSEL, NONE
		CMSEL, S, N_OPERATING_TEMP
		D, ALL, TEMP, OperT
	
		!===========================================================================
		! SOLVE
		!
		ALLSEL, ALL
		SOLVE

	FINISH
	
	/dele,,esav
	/dele,,emat
	/dele,,r001
	/dele,,rdb
	/dele,,full
	
	!==================================================================================================================================
	! POST - WRITE TEMPERATURE FIELD FILE FOR GRAVITY SUPPORT
	!
	/POST1

		!===========================================================================
		! write temperature file
		!
		NSEL, NONE
		ALLSEL, ALL
		NSLE
		*GET,nN,NODE,,COUNT
		*GET,iN,NODE,,NUM,MIN
		*CFOPEN, './_output/GS_T_FIELD', temp
			*DO,i,1,nN
				*GET, T, NODE, iN ,TEMP,
				*VWRITE,'BF,', iN, ', TEMP,', T
%C %8I %C %12.5E

				*GET,iNnew,NODE,iN,NXTH
				iN=iNnew
			*ENDDO
		*CFCLOSE
      	FINISH

	!===============================================================================
	! SAVE DB
	!
	SAVE, , , ,MODEL

*ENDIF

!===========================================================================
!===========================================================================
!
! MECHANICAL ANALYSES
!
!===========================================================================
!===========================================================================
!===========================================================================

!
*IF, COOL_DOWN, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! PRE-PROCESSING
	!
	/PREP7
		CSYS, 0

		!===========================================================================
		! BOUNDARY CONDITIONSs
		!===========================================================================

		!===========================================================================
		! ROTATE CS FOR DOF CONSTRAINED NODES OF CRYO-RING
		!
		! Componetns N_GS_DOF is the bottom of the bearing cradle.
        !
        ESEL, NONE
		NSEL, NONE
		CSYS, GlobCyl
		CMSEL, S, N_GS_DOF
		NROTAT, ALL
		CSYS, 0
		ESEL, NONE
		NSEL, NONE

		!===========================================================================
		! CONSTRAIN ON GRAV SUPP RING
		!
		CMSEL, S, N_GS_DOF
		D, ALL, UX, 0.0
		D, ALL, UY, 0.0
		D, ALL, UZ, 0.0
		ESEL, NONE
		NSEL, NONE

		!===========================================================================
		! INITIALIZE VARIABLES FOR CS AND PF LOADS
		! OWN WEIGHT FOR PF COILS IS OW OF 40° SEGMENT
		! FORCE IS APPLIED FOR EACH 20° SEGMENT. THEREFORE F_PFi = F_PFi_OW/2
		!
		F_CS     =  F_CS_OW
		F_PF2    =  F_PF2_OW/2
		F_PF3    =  F_PF3_OW/2
		F_PF4    =  F_PF4_OW/2
		F_PF5    =  F_PF5_OW/2

        FINISH

	!===========================================================================
	!===========================================================================
	! SOLUTION
	!
	/SOL
	BCSOPTION,,INCORE
        !===========================================================================
		!===========================================================================
		!===========================================================================
		! LS1: PCR PRET + TF DW - SOLUTION SETTINGS
		!===========================================================================
		!===========================================================================
		/ASSIGN, RST, 'MAGNET_MECH',rst
		/TITLE,  'TF_LS1:76 MN radial force + DW TF'
		ANTYPE, STATIC,
		SOLCONTROL, ON, ON
		AUTOTS, ON                         	! AUTO time stepping
		OUTRES, ALL, LAST
		NLDIAG, CONT, ITER                 	! DISPLAY CONTA STATUS AT EACH ITERATION
		NEQIT, 50
		NSUBST, 1, 1, 1

        !===========================================================================
		! REFERENCE AND UNIFORM TEMPERATURE
		!===========================================================================
		!
		ALLSEL,ALL
		TREF, RoomT				! reference Temperature = 292 [K]
		TUNIF, RoomT				! operating Temperature = 292 [K]

		!===========================================================================
		! APPLY PRETENSION
		!===========================================================================
		!
		ID=0 					! section ID counter
		*DO, k, Coil_1, Coil_2

			!===============================================================================
			! APPLY PRETENSIONING ON PCR BOLTS
			!
			*DO, i, 1, 16

				!===========================================================================
				! SET CORRECT PRET FORCE
				!
				*IF, i, LE, 2, OR, i, GE, 15, THEN
					F_PRET = F_PRET_PCR_M80
				*ELSEIF, i, GE, 7, AND, i, LE, 10, THEN
					F_PRET = F_PRET_PCR_M80
				*ELSE
					F_PRET = F_PRET_PCR_M110
				*ENDIF

				!===========================================================================
				! APPLY PRET
				!
				ID=ID+1
				SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
			*ENDDO
		*ENDDO
		!===============================================================================
		! APPLY PRETENSIONING ON OIS BOLTS
		!
		*IF, %arg1%, EQ, 1, THEN
			istart1 = 1
			iend1 = 12
			istart2 = 1
			iend2 = 14
		*ELSEIF, %arg1%, EQ, 2, THEN
			istart1 = 13
			istart2 = 15
			iend1 = 24
			iend2 = 28
		*ELSEIF, %arg1%, EQ, 3, THEN
			istart1 = 1
			iend1 = 24
			istart2 = 1
			iend2 = 28
		*ENDIF
		ID = 7000
		*DO, i, istart1, iend1

			F_PRET = F_PRET_OIS_M130

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		ID = 7024
		*DO, i, istart1, iend1

			F_PRET = F_PRET_OIS_M110

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		ID = 7048

		!===============================================================================
		! APPLY PRETENSIONING ON UPPER M80 GS BOLTS
		!
		*DO, i, istart1, iend1

			F_PRET = F_PRET_UGS_M80

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		ID = 7072
		
		!===============================================================================
		! APPLY PRETENSIONING ON UPPER M60 GS BOLTS
		!
		*DO, i, istart1, iend1

			F_PRET = F_PRET_UGS_M60

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		ID = 7096

		!===============================================================================
		! APPLY PRETENSIONING ON LOWER M85 GS BOLTS
		!
		*DO, i, istart1, iend1

			F_PRET = F_PRET_LGS_M85

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		ID = 7120

		!===============================================================================
		! APPLY PRETENSIONING ON LOWER M60 GS BOLTS
		!
		*DO, i, istart2, iend2

			F_PRET = F_PRET_LGS_M60

			!===========================================================================
			! APPLY PRET
			!
			ID=ID+1
			SLOAD,ID,PL01,LOCK,FORC, F_PRET, 1, 2
		*ENDDO
		!===============================================================================
		! APPLY GRAVITY ACEL ON ALL 3D SOLID ELEMS
		!===============================================================================
		!
		ALLSELL,ALL
		CMACEL, E_SOLIDS, , , g
		Label = ' LS1:PCR+DW'
		TIME, 1
		!===========================================================================
		! KILL ELEMENTS REPRESENTING THE PF1 AND PF6 AND THEIR SUPPORT PLATES
		!
                EKILL,PF1-TF-Connection
                EKILL,PF6-TF-Connection
!		EKILL,PF1
!		EKILL,PF6
		SOLVE

		!===============================================================================
		! LS2: APPLY DW LOAD COMING FROM THE CS AND PF
		!===============================================================================
		!
		/TITLE,  'TF_LS2:76 MN radial force + DW TF + DW CS + DW PF'

		!===========================================================================
		! APPLY LOADS FROM PF AND CS
		!

		!======================================
		! ONLY TYPE B
		!
		*IF, 	 %arg1%, EQ, 1, THEN
			F, N_CS_LOAD, 	FZ, F_CS
                        F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5
                ! ======================================
		! ONLY TYPE A
		!
		*ELSEIF, %arg1%, EQ, 2, THEN
!			cmsel,s,N_PF1_LOAD
!                        *GET,_NNUM,NODE, , COUNT
!                        F, ALL,  FZ, F_PRET_PF1/_NNUM $ALLSEL
                        F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5
!                        cmsel,s,N_PF6_LOAD
!                        *GET,_NNUM,NODE, , COUNT
!                       F, ALL,  FZ, F_PRET_PF6/_NNUM $ALLSEL
		!======================================
		! TYPE A AND B
		!
		*ELSEIF, %arg1%, EQ, 3, THEN
			F, N_CS_LOAD, 	FZ, F_CS
!			cmsel,s,N_PF1_LOAD
!                        *GET,_NNUM,NODE, , COUNT
!                        F, ALL,  FZ, F_PRET_PF1/_NNUM $ALLSEL
                        F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5
!			cmsel,s,N_PF6_LOAD
!                        *GET,_NNUM,NODE, , COUNT
!                        F, ALL,  FZ, F_PRET_PF6/_NNUM $ALLSEL
                *ENDIF
		Label = 'LS2:PFCS_DW'
		TIME, 2
		EALIVE,PF1-TF-Connection
                EALIVE,PF6-TF-Connection
!		EALIVE,PF1
!		EALIVE,PF6
                SOLVE
		!===========================================================================
		! LS3: COOL DOWN
		!===========================================================================
		!
		/TITLE,  'TF_LS3:76 MN radial force + DW + CD'
		ANTYPE, STATIC,
		ALLSEL, ALL
		TREF, RoomT				! reference Temperature = 292 [K]
		TUNIF, OperT				! operating Temperature = 4 [K]
		ESEL, NONE
		NSEL, NONE
		CMSEL, S, E_TH_MODEL
		NSLE, S
		*MSG,NOTE
		START OF READING IN TEMPERATURE LOADS
		/NOPR,
		/INPUT, '../THERMAL/_output/GS_T_FIELD', temp
		/GOPR,
        ALLSEL, ALL
		Label = '     LS3:CD'
		TIME, 3
		SOLVE

	FINISH
	!===============================================================================
	! SAVE DB
	!
	SAVE
!
*ENDIF

!===========================================================================
!===========================================================================
! ENERGIZING
!
*IF, ENRG, EQ, TRUE, THEN

	!===========================================================================
	!===========================================================================
	! CONTINUE SOLUTION APPLYING LORENTZ FORCES FOR DIFFERENT SCENARIOS
	!
	/SOL
        BCSOPTION,,INCORE
		
		!===========================================================================
		! LOOP ON DIFFERENT LOADING CONDITIONS
		!
		*IF,    %arg3%, EQ, 1, THEN,
			StepName  ='L4_TF_ONLY'
			OUTname   ='SOD'
			Label	  ='L4_TFONLY'
			Suffix   = 'r002'

		*ELSEIF, %arg3%, EQ, 2, THEN,
    		StepName  ='L5_SOD'
			OUTname   ='SOP'
			Label	  ='L5_SOD'
			Suffix   = 'r003'

		*ELSEIF, %arg3%, EQ, 3, THEN,
			StepName  ='L6_SOP'
			OUTname   ='XPF'
			Label	  ='L6_SOP'
			Suffix   = 'r004'

		*ELSEIF, %arg3%, EQ, 4, THEN,
			StepName  ='L7_XPF'
			OUTname   ='CS10'
			Label	  ='L7_XPF'
			Suffix   = 'r005'

		*ELSEIF, %arg3%, EQ, 5, THEN,
			StepName  ='L8_CS10'
			OUTname   ='CS2U0'
			Label	  ='L8_CS10'
			Suffix   = 'r006'

		*ELSEIF, %arg3%, EQ, 6, THEN,
			StepName  ='L9_CS2U'
			OUTname   ='CS2L0'
			Label	  ='L9_CS2U'
			Suffix   = 'r007'
                        
       		*ELSEIF, %arg3%, EQ, 7, THEN,
			StepName  ='L10_CS2L0'
			OUTname   ='SOF'
			Label	  ='L10_CS2L0'
			Suffix   = 'r008'

		*ELSEIF, %arg3%, EQ, 8, THEN,
			StepName  ='L11_SOF'
			OUTname   ='SOB'
			Label	  ='L11_SOF'
			Suffix   = 'r009'

		*ELSEIF, %arg3%, EQ, 9, THEN,
			StepName  ='L12_SOB'
			OUTname   ='EOB'
			Label	  ='L12_SOB'
			Suffix   = 'r010'

		*ELSEIF, %arg3%, EQ,10, THEN,
			StepName  ='L13_EOB'
			OUTname   ='EOP'
			OUTname2  ='EOB+PD'
			Label	  ='L13_EOB'
			Suffix   = 'r011'

		*ELSEIF, %arg3%, EQ, 11, THEN,
			StepName  ='L14_EOP'
			Label	  ='L14_EOP'

		*ELSEIF, %arg3%, EQ, 12, THEN,
			StepName  ='L15_EOB+PD'
			Label	  ='L15_EOB+PD'
		*ENDIF

		!===========================================================================
		! correct time for EOB+PD
		!
		*IF, %arg3%, EQ, 12, THEN,
			LStime = 14
		*ELSE
			LStime = %arg3%+2
		*ENDIF

		!===========================================================================
		! SOLUTION SETTINGS
		!
		PARSAV, SCALAR, ScalarPARAM, PARM
		*IF,%arg3%,EQ,12,THEN
			/ASSIGN, RST, 'MAGNET_EOB',rst
		*ELSE
			/ASSIGN, RST, 'MAGNET_MECH',rst	
		*ENDIF
		
		ANTYPE, STATIC, REST, LAST, , CONTINUE
		PARRES, CHANGE, ScalarPARAM, PARM
		
		/TITLE,%StepName%  %RefCaseName%_Rev%Rev%  Scenario2:15MA Baseline

		ALLSEL, ALL
		TIME, LStime+1
		
		        
		!===========================================================================
		! READ PF LOADS FROM FILE
		!
		*DIM, PF_LOADS, ARRAY, 6, 3
		*VREAD, PF_LOADS(1,1),   '../../../MAG_15MA_2016/2Coil/%RefCaseName%/%Scenario%_SUMPF', forces, , JIK, 3 ,6
(3E13.5)

		!===========================================================================
		! READ CS LOADS FROM FILE
		!
		*DIM, CS_LOADS, ARRAY, 6, 3
		*VREAD, CS_LOADS(1,1),   '../../../MAG_15MA_2016/2Coil/%RefCaseName%/%Scenario%_SUMCS', forces, , JIK, 3 ,6
(3E13.5)
		CS_LOAD = 0
		*DO,i,1,6
			CS_LOAD=CS_LOAD+CS_LOADS(i,3)
		*ENDDO
                !===========================================================================
		! INITIALIZE VARIABLES
		!
		F_CS       =  CS_LOAD+F_CS_OW
		F_FRIC_PF1 =  (-1.0)*(PF_LOADS(1,3)+F_PRET_PF1+F_PF1_OW)*MU_PF
                F_PF2      =  (PF_LOADS(2,3)+F_PF2_OW)/2
		F_PF3      =  (PF_LOADS(3,3)+F_PF3_OW)/2
		F_PF4      =  (PF_LOADS(4,3)+F_PF4_OW)/2
		F_PF5      =  (PF_LOADS(5,3)+F_PF5_OW)/2
                F_FRIC_PF6 =  (PF_LOADS(6,3)+F_PRET_PF6+F_PF6_OW)*MU_PF
                !
                *IF,%arg3%,EQ, 1,OR,%arg3%,EQ,11, THEN  ! Make Friction 0 for TFonly and EOP.
                F_FRIC_PF1 = 0.0
                F_FRIC_PF6 = 0.0
                *ENDIF
                *IF,%arg3%,EQ,10,OR,%arg3%,EQ,12,THEN  ! Reverse radial direction for EOB and EOB+PD
                F_FRIC_PF1 = PF_LOADS(1,1)/2
                *ENDIF
                !===========================================================================
		! CONVERGENCE SETTINGS
		!
		SOLCONTROL, ON, ON
		AUTOTS, ON                                ! AUTO time stepping
		OUTRES, ALL, LAST
		NLDIAG, EFLG, OFF
!		NLDIAG, NRRE, ON
        NLDIAG, CONT, ITER        ! DISPLAY CONTA STATUS AT EACH ITERATION
        NSUBST, 1, 50, 1
        NEQIT, 30
        !===========================================================================
		! APPLY LOADS FROM PF AND CS
		!

		!======================================
		! ONLY TYPE B
		!
		*IF, 	 %arg1%, EQ, 1, THEN
			F, N_CS_LOAD, 	FZ, F_CS
			F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5

		!======================================
		! ONLY TYPE A
		!
		*ELSEIF, %arg1%, EQ, 2, THEN
			!
                        cmsel,s,N_PF1_LOAD
                        *get,_NNUM,node, ,count
                        F,ALL,  FX, F_FRIC_PF1/_NNUM
                        cmsel,s,N_PF6_LOAD
                        *get,_NNUM,node, ,count
                        F,        ALL,  FX, F_FRIC_PF6/_NNUM
                        allsel
                        !
                        F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5
                        !

		!======================================
		! TYPE A AND B
		!
		*ELSEIF, %arg1%, EQ, 3, THEN
			F, N_CS_LOAD, 	FZ, F_CS
			!
                        cmsel,s,N_PF1_LOAD
                        *get,_NNUM,node, ,count
                        F,ALL,  FX, F_FRIC_PF1/_NNUM
                        !
                        cmsel,s,N_PF6_LOAD
                        *get,_NNUM,node, ,count
                        F,ALL,  FX, F_FRIC_PF6/_NNUM
                        allsel
                        !
                        F, N_PF2_LOAD, 	FZ, F_PF2
			F, N_PF3_4_LOAD,FZ, F_PF3+F_PF4
			F, N_PF5_LOAD, 	FZ, F_PF5
                        !
                        *ENDIF


		!===========================================================================
		! APPLY LORENTZ FORCES FROM MAG ANALYSIS
		!
		ALLSELL, ALL
		!================
		! ON TF COIL
		!

		*MSG,NOTE
		START OF READING IN LORENTZ FORCES ON TF-COILS
        /NOPR,
		/INPUT, '../../../MAG_15MA_2016/2Coil/%RefCaseName%/%Scenario%_TF', forces
		/GOPR,
		
		!================
		! ON PF COIL
		!
		
		*IF,%arg1%,EQ,2,OR,%arg1%,EQ,3,THEN
			*MSG,NOTE
			START OF READING IN LORENTZ FORCES on PF-COILS
			/NOPR,
			/INPUT, '../../../MAG_15MA_2016/2Coil/%RefCaseName%/%Scenario%_PF', forces
			/GOPR,
		*ENDIF
		
        SOLVE
	FINISH

	!===============================================================================
	! SAVE DB
	!
	SAVE
	
	! SAVE EOB RST FOR EOB+PD
	*IF,%arg3%,EQ,10,AND,%arg4%,EQ,12,THEN
		/COPY, './MAGNET_MECH', rst, , './MAGNET_EOB', rst
!		*DO,TMP,0,19
!			/COPY, './MAGNET_MECH%TMP%', rst, , './MAGNET_EOB%TMP%', rst
!		*ENDDO
	*ENDIF

	!============================================================
	! POST 
	! ASSIGN LOADCASE EOB+PD to MAGNET_MECH.rst
	*IF,%arg3%,EQ,12,THEN
		/POST1
		LCSUM,ALL
		FILE,'MAGNET_EOB','rst','.'
		set,,,,,15
		FILE,'MAGNET_MECH','rst','.'
		RAPPEND, 15, 15
	*ENDIF

*ENDIF

! GO BACK TO VERSION FOLDER FOR NEXT MACRO FILE !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
FINISH																		!
/CLEAR,START																!
/CWD, '../../'																!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!===============================================================================
! EOF CONDITION

/EOF







	