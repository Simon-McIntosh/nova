readme
! Scripts to extract nodal stress and displacment results.
!
! Example
! -------
! Run following in post after solution.
!
! *ulib,substress,ans
! *use,get_part,'key_sections'
! *use,write_stress,'key_sections'
!
! Replace label with user defined nammed selection(s).
! Script can handle nammed selections containing multiple parts
/eof
get_part
! get_part - derived from find_part
! macro identifies seperate patches based on material id and face connections
! requires get_patch
! arg1 nammed selection containing all patches - tests for plural, arg1+'s'
/nopr  ! dissable printout
part_name=arg1
material_sort=0  ! sort patches by material 0==no, 1==yes
nfeature=100  ! maximum average feature number per material
np=0  ! initalize part number
/prep7
nsel,none
nsel,s,node,,%part_name%
*get,nnd,node,0,count
*if,nnd,eq,0,then
  nsel,s,node,,%part_name%s  ! try plural
  *get,nnd,node,0,count
*endif
*if,nnd,eq,0,then
  *msg,warn,part_name,part_name
  no nodes found in '%C' or '%Cs' nammed selections
*else
  esln
  esel,u,ename,,170,175  ! exclude contacts
  *get,netyp,etyp,0,num,max
  cid = netyp+1  ! dummy mesh elements to define surface
  et,cid,174
  type,cid
  mat,cid
  real,cid
  *get,nel_o,elem,0,num,maxd
  esurf  ! create surface contact elements
  *get,nel_1,elem,0,num,maxd
  esel,s,elem,,nel_o+1,nel_1
  cm,%part_name%_surface,elem
  csys,1
  *if,material_sort,eq,1,then
    *get,nmat,mat,0,num,max
    *dim,part_angle_nmat,array,nmat*nfeature
    *do,nm,1,nmat
      esel,none
      esel,s,mat,,nm
      get_patch
    *enddo
  *else
    nmat = 10
    *dim,part_angle_nmat,array,nmat*nfeature
    esel,all
    get_patch
  *endif
  edele,%part_name%_surface  ! delete dummy surface
  et,cid,0  ! dissable elements
  allsel
  *dim,part_angle,array,np  ! toroidal sort
  *vfun,part_angle,copy,part_angle_nmat
  *del,part_angle_nmat
  *dim,part_index,array,np
  *vfill,part_index,ramp,1,1
  *moper,part_index_o,part_index,sort,part_angle
  *del,part_index_o
  *do,i,1,np  ! name parts in toroidal order
    nsel,none
    cmsel,s,%part_name%%part_index(i)%_tmp
    cm,%part_name%%i%,node
    %part_name%%i%_da = %part_name%%part_index(i)%_da_tmp  ! store area
  *enddo
  *do,i,1,np  ! delete tempary groups
    cmdele,%part_name%%i%_tmp
    *del,%part_name%%i%_da_tmp
  *enddo
  *del,part_angle
  *del,part_index
  csys,0
*endif
%part_name%_number=np  ! store number of parts found
n%part_name%=np  ! alternate parameter name
*msg,note,part_name,np
Found %G patches in '%C' nammed selection
allsel
/eof
get_patch
! get_patch - derived from find_patch
! subfunction called by get_part
nsle
nsel,r,node,,%part_name%s  ! reselect from group
esln,s,1
esel,r,elem,,%part_name%_surface
nsle
*get,nnd,node,0,count
*if,nnd,gt,0,then
  cm,surface_group,elem  ! group of surface elements
  *get,nel,elem,0,count
  *dowhile,nel
    esel,s,elem,,elnext(0)  ! pivot element
    nel_o = 1
    nel_diff = 1
    *dowhile,nel_diff
      nsle
      esln
      esel,r,elem,,surface_group
      *get,nel_1,elem,0,count
      nel_diff = nel_1 - nel_o
      nel_o = nel_1
    *enddo
    nsle  ! store patch nodes
    np = np + 1
    *if,np,gt,nmat*nfeature,then
      *msg,error
        increase feature number 'nfeature' in find_part
    *endif
    cm,%part_name%%np%_tmp,node
    *vget,esel_mask,elem,1,esel
    *vmask,esel_mask
    *vget,el_area,elem,1,geom
    *vmask,esel_mask
    *vscfun,%part_name%%np%_da_tmp,sum,el_area
    *del,esel_mask
    *del,el_area
    *get,nd_mxloc,node,0,mxloc,y
    *get,nd_mnloc,node,0,mnloc,y
    part_angle_nmat(np) = (nd_mxloc + nd_mnloc) / 2
    cm,surface_patch,elem  ! remove patch from surface surface group
    cmsel,s,surface_group
    cmsel,u,surface_patch
    cm,surface_group,elem
    *get,nel,elem,0,count  ! count remaining elements
  *enddo
  cmdele,surface_group
  cmdele,surface_patch
*endif
/eof
write_stress
part=arg1
*if,n%part%,gt,0,then
  xx = 'x','y','z','xy','yz','xz'
  /post1
  set,last
  *get,nLS,active,0,solu,ncmls  ! number of load steps
  *do,LS,1,nLS
    set,LS  ! read load step into database
    *cfopen,%part%_reaction_LS%LS%,txt  ! open file and write header
    *vwrite,'x','y','z','ux','uy','uz','sx','sy','sz','sxy','syz','sxz'
    (12(A12,X))
    *do,nr,1,%part%_number
      *vwrite,'%part%%nr-1%'
      (A8)
      *use,write_tensor,'%part%%nr%'
    *enddo
    *cfclose
  *enddo
  allsel
*endif
*del,xx
/eof
write_tensor
nsel,none
nsel,s,node,,%arg1%
esln
esel,u,ename,,170,175  ! deselect contacts
esel,u,ename,,179  ! deselect pre-tension
esel,u,ename,,181  ! deselect surface
esel,u,ename,,281
nsle,s,corner  ! only corner nodes
nsel,r,node,,%arg1%  ! re-select from original
*vget,nd_mask,node,1,nsel  ! node mask
*get,nnd,node,0,count
*dim,cent,array,nnd,3  ! location
*dim,disp,array,nnd,3  ! Dissplacment
*dim,sigma,array,nnd,6  ! stress tensor
*do,j,1,3  ! node location and dissplacment
  *vmask,nd_mask
  *vget,x,node,1,loc,%xx(j)%
  *vget,u,node,1,u,%xx(j)%
  *vmask,nd_mask
  *vfun,cent(1, j),comp,x
  *vmask,nd_mask
  *vfun,disp(1, j),comp,u
*enddo
*do,j,1,6  ! stress tensor
  *vmask,nd_mask
  *vget,s,node,1,s,%xx(j)%
  *vmask,nd_mask
  *vfun,sigma(1, j),comp,s
*enddo
*vmask,nd_mask
*vwrite,cent(1,1),cent(1,2),cent(1,3),disp(1,1),disp(1,2),disp(1,3),sigma(1,1),sigma(1,2),sigma(1,3),sigma(1,4),sigma(1,5),sigma(1,6)
(12(E12.6,X))
*del,cent
*del,disp
*del,sigma
*del,nd_mask
/eof
